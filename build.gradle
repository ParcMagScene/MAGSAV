plugins {
    id 'org.springframework.boot' version '3.2.12' apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
    id 'org.openjfx.javafxplugin' version '0.1.0' apply false
    id 'com.github.node-gradle.node' version '7.0.1' apply false
}

allprojects {
    group = 'com.magscene'
    version = '3.0.0'
    
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

subprojects {
    // N'appliquer le plugin Java que pour les modules Java (pas web-frontend)
    if (project.name != 'web-frontend') {
        apply plugin: 'java'
        
        java {
            sourceCompatibility = JavaVersion.VERSION_21
            targetCompatibility = JavaVersion.VERSION_21
        }
        
        tasks.withType(JavaCompile) {
            options.encoding = 'UTF-8'
            options.compilerArgs += ['-parameters']
        }
    }
    
    // Configuration commune pour tous les sous-projets
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    
    // Gestion des versions communes
    ext {
        springBootVersion = '3.1.12'
        javafxVersion = '21'
        junitVersion = '5.10.0'
        nodeVersion = '18.17.0'
        reactVersion = '18.2.0'
    }
}

// Configuration sp√©cifique pour les modules
configure(project(':backend')) {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
}

configure(project(':desktop-javafx')) {
    apply plugin: 'org.openjfx.javafxplugin'
}

wrapper {
    gradleVersion = '8.4'
    distributionType = Wrapper.DistributionType.BIN
}